plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '8.1.1'
}

repositories {
    maven { url 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/' }
    maven { url 'https://oss.sonatype.org/content/repositories/snapshots' }
    maven { url 'https://oss.sonatype.org/content/repositories/central' }
    maven { url 'https://repo.extendedclip.com/content/repositories/placeholderapi/' }
    maven { url 'https://repo.codemc.io/repository/maven-public/' }
    maven { url 'https://repo.inventivetalent.org/content/groups/public/' }
    mavenCentral()
}

processResources {
    duplicatesStrategy = DuplicatesStrategy.INCLUDE
    from(sourceSets.main.resources.srcDirs) {
        include 'plugin.yml'
        expand(rootProject.properties)
    }
}

dependencies {
    compileOnly 'org.projectlombok:lombok:1.18.22'
    annotationProcessor 'org.projectlombok:lombok:1.18.22'

    compileOnly 'org.spigotmc:spigot-api:1.14.4-R0.1-SNAPSHOT'
    compileOnly 'me.clip:placeholderapi:2.10.9'

    // shaded dependencies
    implementation 'redis.clients:jedis:3.5.1'
    implementation 'org.apache.commons:commons-pool2:2.4.2'
    implementation 'com.zaxxer:HikariCP:4.0.2'
    implementation 'org.slf4j:slf4j-nop:1.7.32'
    implementation 'org.bstats:bstats-bukkit:1.7'

    compileOnly fileTree(dir: 'libs', includes: ['*.jar'])
}

compileJava {
    options.encoding = 'UTF-8'
}

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}

jar {
    enabled = false
    dependsOn (shadowJar)
}

shadowJar {
    archiveClassifier.set('')
    relocate 'redis.clients.jedis', project.group + '.shaded.jedis'
    relocate 'org.apache.commons.pool2', project.group + '.shaded.commons.pool2'
    relocate 'com.zaxxer.hikari', project.group + '.shaded.hikari'
    relocate 'org.slf4j', project.group + ".shaded.slf4j"
    relocate 'org.bstats', project.group + '.shaded.bstats'
    minimize()
}