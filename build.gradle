plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '6.1.0'
}

group 'me.realized.tokenmanager'
version '3.2.6-UNOFFICIAL'

processResources {
    duplicatesStrategy = DuplicatesStrategy.INCLUDE
    from(sourceSets.main.resources.srcDirs) {
        include '**/*.yml'
        expand(
                'version': project.version
        )
    }
}

repositories {
    // Spigot
    maven { url 'https://hub.spigotmc.org/nexus/content/repositories/snapshots' }
    // Bungeecord
    maven { url 'https://oss.sonatype.org/content/repositories/snapshots' }
    // Vault
    maven { url 'https://jitpack.io' }
    // PlaceholderAPI
    maven { url 'https://repo.extendedclip.com/content/repositories/placeholderapi/' }
    // Inventive repo
    maven { url 'https://repo.inventivetalent.org/content/groups/public/' }
    mavenCentral()
}

dependencies {
    compileOnly ('org.spigotmc:spigot-api:1.17-R0.1-SNAPSHOT') {
        exclude group: 'net.md-5', module: 'bungeecord-chat'
        exclude group: 'org.yaml', module: 'snakeyaml'
    }
    compileOnly ('com.github.MilkBowl:VaultAPI:1.7') {
        exclude group: 'org.bukkit', module: 'bukkit'
    }
    compileOnly 'me.clip:placeholderapi:2.10.9'

    // shaded dependencies
    implementation 'redis.clients:jedis:2.9.0'
    implementation 'org.apache.commons:commons-pool2:2.4.2'
    implementation ('com.zaxxer:HikariCP:2.7.8') {
        exclude group: 'org.slf4j', module: 'slf4j-api'
    }
    implementation 'org.slf4j:slf4j-nop:1.7.25'
    implementation ('org.inventivetalent.spiget-update:bukkit:1.4.2-SNAPSHOT') {
        exclude group: 'org.bukkit', module: 'bukkit'
    }
}

shadowJar {
    relocate 'redis.clients', project.group + '.lib.redis'
    relocate 'org.apache.commons.pool2', project.group + '.lib.commons'
    relocate 'com.zaxxer.hikari', project.group + '.lib.hikari'
    relocate 'org.slf4j', project.group + '.lib.slf4j'
    relocate 'org.inventivetalent.update.spiget', project.group + '.lib.spiget'
    archiveClassifier.set('')
}

tasks.build {
    dependsOn(tasks.shadowJar)
}